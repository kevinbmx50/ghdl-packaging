_realname=ghdl-llvm-git
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=git
pkgrel=1
pkgdesc="GHDL: the open-source analyzer, compiler, simulator and (experimental) synthesizer for VHDL (LLVM backend) (mingw-w64)"
arch=('any')
license=(['GPLv2+', 'custom'])
url="http://ghdl.free.fr/"
provides=("${MINGW_PACKAGE_PREFIX}-ghdl")
conflicts=(
  "${MINGW_PACKAGE_PREFIX}-ghdl-mcode"
  "${MINGW_PACKAGE_PREFIX}-ghdl-llvm"
  "${MINGW_PACKAGE_PREFIX}-ghdl-mcode-rc"
  "${MINGW_PACKAGE_PREFIX}-ghdl-llvm-rc"
  "${MINGW_PACKAGE_PREFIX}-ghdl-mcode-git"
)
depends=('zlib-devel' "${MINGW_PACKAGE_PREFIX}-clang")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada")
source=(
  "ghdl::git://github.com/ghdl/ghdl.git"
)
sha512sums=(
  'SKIP'
)

pkgver() {
  cd "${srcdir}/ghdl"
  # Date of the last git commit
  _verdate=`git log -1 --date=short --pretty=format:%cd`
  if `git describe --exact-match > /dev/null 2>&1` ; then
    echo "`git describe --tags`.$_verdate" | sed 's/-//g'
  else
    git describe --tags --abbrev=10 | sed "s/\([!-]*\)-\(.*\)/\1.$_verdate.\2/g" | sed 's/-//g'
  fi;
}

build() {
  echo '::group::Build'
    mkdir "${srcdir}/builddir"
    cd "${srcdir}/builddir"
    ../ghdl/configure \
      --prefix=${MINGW_PREFIX} \
      --with-llvm-config="llvm-config --link-static" \
      LDFLAGS="-static" \
      --enable-libghdl \
      --enable-synth
    make GNATMAKE="gnatmake -j$(nproc)"
  echo '::endgroup::'
}

check() {
  cd "${srcdir}/builddir"
  make install.vpi.local
  make test
}

package() {
  echo '::group::Package'
    cd "${srcdir}/builddir"
    mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"
    make DESTDIR="${pkgdir}" install
  echo '::endgroup::'
}
